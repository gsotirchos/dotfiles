;;; flymake-mypy.el --- mypy support for flymake -*- lexical-binding: t -*-

;; Author: jason <jason@zzq.org>
;; Created: 26 Sep 2022
;; Version: 0.3.1

;;; Commentary:

;; This module provides support for the mypy static type checker using flymake.

;; Usage:
;;   (require 'flymake-mypy)
;;   (add-hook 'python-mode-hook #'flymake-mypy-enable)

;; Changes:
;;   0.3.1
;;     - fixes issue with (project-current)'s shape changing which prevents
;;       flymake-mypy from starting
;;   0.3.0
;;     - uses mypy's --show-error-end for building more accurate error ranges
;;       (Requires mypy >= 0.981)
;;     - fixes issues with mypy errors causing minor freezing while editing
;;   0.2.0
;;     - use async processing
;;   0.1.0
;;     - initial release

;;; Code:
(require 'project) ;; for project support

(defvar flymake-mypy-executable "python -mmypy"
  "The mypy executable to use for syntax checking.")

(defvar flymake-mypy-output-pattern "^\\(.*\\.py\\):\\([0-9]+?\\):\\(?:\\([0-9]+?\\):\\([0-9]+?\\):\\([0-9]+?\\):\\)? \\(.*?\\): \\(.*\\)$"
  "The regex to use for parsing mypy output.")

(defvar-local flymake-mypy--proc nil)

(defun flymake-mypy-enable ()
  "Enable the Mypy checker for Flymake."
  (interactive)
  (add-hook 'flymake-diagnostic-functions 'flymake-mypy--run nil t))

(defun flymake-mypy-disable ()
  "Disable the Mypy checker for Flymake."
  (interactive)
  (remove-hook 'flymake-diagnostic-functions 'flymake-mypy--run nil t))

(defun flymake-mypy--get-position (buffer line column)
  "Calculate position for the given LINE and COLUMN in the BUFFER."
  (interactive)
  (with-current-buffer buffer
    (save-excursion
      (goto-char (point-min))
      (forward-line (1- line))  ;; starting from line 1
      (move-to-column column)
      (point))))

(defun flymake-mypy--run (report-fn &rest _args)
  "Run flymake-mypy reporting diagnostics using the REPORT-FN."
  ;; Patched (upstream: probes "python"): check the checker that is actually
  ;; run.  Images commonly ship python3 without a `python' alias, which made
  ;; the backend disable itself in a container even with mypy present.
  (let ((executable (car (split-string flymake-mypy-executable " "))))
    (unless (if (file-name-absolute-p executable)
                ;; `executable-find' only searches for bare names, and the
                ;; path is already absolute once resolved on the remote host.
                (file-executable-p (concat (or (file-remote-p default-directory) "")
                                           executable))
              (executable-find executable))
      (error "Cannot find the `%s' executable for flymake-mypy" executable)))
  (let ((source-buffer (current-buffer)))
    (save-restriction
      (widen)
      ;; Patched (upstream: `make-temp-file'): the shadow file has to be
      ;; created on the host mypy will run on, which for a buffer visiting a
      ;; container is the container.
      (let* ((temp-file (concat (make-nearby-temp-file "flymake-mypy") ".py"))
             ;; Patched (upstream: (car (last (project-current)))): run from
             ;; the file's own directory for buffers outside any project.
             (default-directory (if-let* ((proj (project-current)))
                                    (project-root proj)
                                  default-directory)))
        (write-region (point-min) (point-max) temp-file nil 'quiet)

        (setq flymake-mypy--proc
              (make-process
               :name "flymake-mypy"
               :noquery t
               ;; Patched: without this `make-process' spawns locally even
               ;; when `default-directory' is remote.
               :file-handler t
               :connection-type 'pipe
               :buffer (generate-new-buffer "*flymake-mypy-output*")
               :command (mapcar (lambda (x) (shell-quote-argument x))
                                (flatten-list (list (split-string flymake-mypy-executable " ")
                                                    ;; Patched: Tramp gives
                                                    ;; remote processes a PTY,
                                                    ;; and colour codes would
                                                    ;; defeat the output regexp.
                                                    "--no-color-output"
                                                    "--show-column-numbers"
                                                    "--show-error-end"
                                                    "--show-absolute-path"
                                                    "--shadow-file"
                                                    ;; Patched: local names,
                                                    ;; since mypy may be
                                                    ;; running in a container.
                                                    (file-local-name
                                                     (buffer-file-name source-buffer))
                                                    (file-local-name temp-file)
                                                    (file-local-name
                                                     (buffer-file-name source-buffer)))))
               :sentinel
               (lambda (proc _event)
                 (when (memq (process-status proc) '(exit signal))
                   (unwind-protect
                       ;; If the buffer local var for this process matches proceed
                       (if (with-current-buffer source-buffer (eq proc flymake-mypy--proc))
                           (with-current-buffer (process-buffer proc)
                             ;; Patched: a remote tty reports CRLF.
                             (goto-char (point-min))
                             (while (search-forward "\r" nil t)
                               (replace-match "" nil t))
                             (goto-char (point-min))
                             (cl-loop
                              while (search-forward-regexp flymake-mypy-output-pattern nil t)
                              for line = (match-string 0)
                              for filename = (match-string 1)
                              for line-num = (string-to-number (match-string 2))
                              ;; mypy column numbers are off by 1
                              for col-num = (1- (string-to-number (or (match-string 3) "1")))
                              for end-line-num = (string-to-number (or (match-string 4) "1"))
                              for end-col-num = (string-to-number (or (match-string 5) "1"))
                              for error-level = (match-string 6)
                              for description  = (format "Mypy (%s): (%s)" error-level (match-string 7))
                              for flymake-err-type = (cond ((string-equal error-level "error") :error)
                                                           ((string-equal error-level "warning") :warning)
                                                           (t :note))
                              collect
                              ;; for some reason mypy will sometimes randomly include messages
                              ;; for files not asked for
                              ;; Patched: mypy reports the path it was given,
                              ;; which for a container buffer is the name
                              ;; inside the container rather than the Tramp
                              ;; one; without stripping the prefix every
                              ;; diagnostic looks like it belongs to another
                              ;; file and Flymake files it away as foreign.
                              (if (string-equal
                                   (file-local-name (buffer-file-name source-buffer))
                                   filename)
                                  (progn
                                    (let* ((beg-region (flymake-mypy--get-position
                                                        source-buffer
                                                        line-num
                                                        col-num))
                                           (end-region (flymake-mypy--get-position
                                                        source-buffer
                                                        (max line-num end-line-num)
                                                        (if (> end-line-num line-num)
                                                            end-col-num
                                                          (max col-num end-col-num)))))

                                      (flymake-make-diagnostic
                                       source-buffer
                                       beg-region
                                       end-region
                                       flymake-err-type
                                       description)))
                                (flymake-make-diagnostic
                                 filename
                                 (cons line-num col-num)
                                 (cons line-num (1+ col-num))
                                 flymake-err-type
                                 description))
                              into diags
                              finally (progn
                                        (if diags
                                            (funcall report-fn diags)
                                          (funcall report-fn (list))))))
                         (flymake-log :warning "Canceling obsolete check %s" proc))
                     ;; unwind protect is similar to try/finally. this is the finally clause
                     (kill-buffer (process-buffer proc)))))))))))

(provide 'flymake-mypy)

;;; flymake-mypy.el ends here
